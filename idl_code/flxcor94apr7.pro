FUNCTION FLXCOR94apr7,WAVE,FLX
;+
;
; PURPOSE:
;	CORRECT UV FLUXES TO THE WD SCALE IN THE 1140-4400A RANGE. THESE 
;	PRELIMINARY CORRECTIONS ARE BASED ON FOS HI-DISP BLUE OBS AND FINLEY 
;	MODEL*1.005x FOR G191B2B ONLY.  rcb
;
;calling sequence:
;	CORRECTED_FLUX=FLXCOR94APR7(WAVE,FLX)
;
; input: WAVE-WAVELENGTH ARRAY OF FLUX VECTOR TO BE CORRECTED
;	 FLX-CORRESPONDING FLUX VECTOR TO BE CORRECTED
; output-THE FLX SPECTRUM CONVERTED TO THE WD STANDARD SPECTROPHOTOMETRY SCALE.
;
; HISTORY:
;	93DEC8-RCB
;	93DEC14-UPDATE SPLINE NODES
;	94MAR13-UPDATE SPLINE NODES
;	94MAR31-UPDATE SPLINE NODES AND CHANGE LOWER LIMIT FROM 1140 TO 1150
;	94apr7 -tiny update for small ignore_data bug
; DO *NOT* CHANGE THIS VERSION, AS IT IS USED IN THE STEPS TO THE PERFECT 
;	FLUX STANDARDS TO GET THE *.1STWD TABLES. SEE MAKSTD.PRO.
;-
; TABLE OF x spline nodes for g191b2b blue fos hi-disp
WFIT=[									$
1168.0,1238.0,1308.0,1378.0,1448.0,1518.0,1588.0,1658.0,1728.0,1798.0,$
1868.0,1938.0,2008.0,2078.0,2148.0,2218.0,2288.0,2358.0,2428.0,2498.0,$
2568.0,2638.0,2708.0,2778.0,2848.0,2918.0,2988.0,3058.0,3128.0,3198.0,$
3268.0,3338.0,3408.0,3478.0,3548.0,3618.0,3688.0,3758.0,3828.0,3898.0]

; TABLE OF y spline nodes for g191b2b blue fos hi-disp
CFIT=[									$
1.0622,1.0205,0.9808,0.9020,0.9130,0.9486,0.8927,0.8884,0.9168,0.9145,$
0.8982,0.8835,0.9300,0.9521,0.9519,0.9193,0.9335,0.9449,0.9642,0.9696,$
0.9558,0.9600,0.9532,0.9566,0.9525,0.9597,0.9635,0.9706,0.9952,1.0086,$
1.0204,1.0245,1.0181,1.0153,1.0068,0.9934,0.9855,0.9756,0.9878,1.    ]

GOOD=WHERE((WAVE GE 1140) AND (WAVE LE MAX(WFIT)),COUNT)
CORRFLUX=FLX
IF COUNT GT 0 THEN CORRFLUX(GOOD)=FLX(good)/CSPLINE(WFIT,CFIT,WAVE(GOOD))

if min(wave) lt 1140 then begin		;94mar17 to fix voyager in std stars
	GOOD=WHERE((WAVE lt 1140))
	c1140=CSPLINE(WFIT,CFIT,1140.)
	print, 'FLXCOR94apr7 corrected wl lt 1140 by ',c1140(0)
	corrflux(good)=flx(good)/c1140(0)
	endif

RETURN,CORRFLUX
END
