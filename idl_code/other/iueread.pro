PRO IUEREAD,UNIT,FIRST,LAST,NAME
;+
;
; mou/for mua0:
; ass mua0 mt1
;
; PROCEDURE TO ACQUIRE THE IUE [LOW] DISPERSION SPECTRA FROM
; AN INPUT TAPE AND PLACE THEM INTO ST INTERNAL FITS FILES.
;
; D. LINDLER  4/18/84...mod to do hi-disp too 89oct31
; 93DEC9-FIX TO READ LINE X LINE LOW DISP TOO
;
; IUEREAD,UNIT,FIRST,LAST,NAME
;
; INPUT:
;	UNIT - TAPE UNIT NUMBER
;	FIRST,LAST - OPTIONAL INPUTS GIVING THE FIRST AND
;		LAST FILE NUMBERS ON THE TAPE TO BE PROCESSED
;	NAME - OPTIONal INPUT GIVING FIRST CHARACTERS OF FILE NAME
;
; OUTPUT: 
;	EACH FILE WILL BE PLACED IN THE FITS DATA SET
;		NAME<FILE NUMBER>.HHD
; history
; 93may25-convert to v2 rcb/djl
;
;-
IF N_PARAMS(0) EQ 1 THEN BEGIN
   FIRST=1
   LAST=9999
END
IF N_PARAMS(0) LT 4 THEN NAME='IUE'
;
; SKIP FILES IF NECC.
;
IF FIRST GT 1 THEN SKIPF,UNIT,FIRST-1
;
; SET UP DATA AREAS
;
LAB=BYTARR(360)		;VICAR LABEL
DATA=INTARR(1024)	;DATA RECORD
IH=INTARR(1024)		;HEADER RECORD
;
; PROCESS ALL FILES
;
FOR FILE=FIRST,LAST DO BEGIN
;
  ON_IOERROR,FIN
  TAPRD,LAB,UNIT		;READ LABEL
  nbyt=!err		;!err seems to get reset by the next lines in v2
  LABS=STREBCASC(STRING(LAB))	;CONVERT TO ASCII
  PRINT,FILE,STRMID(LABS,0,70)
  !err=nbyt
  WHILE !err EQ 360 DO TAPRD,IH,UNIT,1	;READ IUE HEADER RECORD
;
; DETERMINE GROUP SIZE
;
  IF IH(58) EQ 0 THEN IH(58)=5		;FIX FOR LOW DISPERSION ONLY
  NRECS=IH(7)
  NPOINTS=IH(302)		;NUMBER OF POINTS IN EACH RECORD
  norders = ih(4)>1		;number of orders present
  GROUP=FLTARR(1022,NRECS)	;STORAGE FOR EVERYTHING
  
;
;
; CREATE FITS HEADER
;
HEAD=STRARR(25)
HEAD(0)='END'+string(replicate(32b,77))
sxaddPAR,HEAD,'SIMPLE','F'
sxaddPAR,HEAD,'BITPIX',32
sxaddPAR,HEAD,'NAXIS',2,'TWO DIMENSIONAL DATA'
sxaddPAR,HEAD,'NAXIS1',NPOINTS,'NUMBER OF POINTS IN SPECTRA'
sxaddPAR,HEAD,'NAXIS2',NRECS,'NUMBER OF IUE RECORDS IN GROUP'
sxaddPAR,HEAD,'DATATYPE','REAL*4','FLOATING POINT DATA'
sxaddPAR,HEAD,'GROUPS','T','GROUP FORMAT'
sxaddPAR,HEAD,'GCOUNT',NORDERS,'ONE GROUP FOR LOW DISPERSION SPECTRA'
sxaddPAR,HEAD,'TAPEFILE',FILE,'FILE NUMBER NO INPUT TAPE'
;
; ADD VICAR LABEL
;
FOR I=0,4 DO HEAD(8+I)='HISTORY '+STRMID(LABS,I*72,71)
HEAD(13)='END'+string(replicate(32b,77))
;
; ADD SELECTED INFO FROM IUE HEADER RECORD
;
sxaddPAR,HEAD,'WMIN',IH(2),'MINIMUM WAVELENGTH'
sxaddPAR,HEAD,'WMAX',IH(3),'MAXIMUM WAVELENGTH'
sxaddPAR,HEAD,'RAHOUR',IH(44),'RIGHT ASCENSION HOUR'
sxaddPAR,HEAD,'RAMIN',IH(45),'                MINUTES'
sxaddPAR,HEAD,'RASEC',IH(46)/10.0,'                SECONDS'
sxaddPAR,HEAD,'DECDEG',IH(47),'DECLINATION DEGREES'
sxaddPAR,HEAD,'DECMIN',IH(48),'            MINUTES'
sxaddPAR,HEAD,'DECSEC',IH(49),'            SECONDS'
sxaddpar,head,'ORDER1',IH(202),'ORDER NUMBER OF FIRST GROUP'
;
;
; PROCESS EACH RECORD
;
FOR GNUM = 1,NORDERS DO BEGIN
     FOR I=0,NRECS-1 DO BEGIN
   	TAPRD,DATA,UNIT,1
   	IF NORDERS LE 1 THEN D=FLOAT(DATA(2:NPOINTS+1)) $
		        ELSE D=FLOAT(DATA(2:1023))
   	IF I EQ 0 THEN BEGIN
		D=D/IH(58)	;SCALE FACTOR FOR WAVELENGTHS
		IF NORDERS GT 1 THEN BEGIN
;
; THIS LINE REPLACED BY FOLLOWING LINE TO GET IT TO WORK WITH LINEXLINE
; FILES  DEC 9, 1993  DJL
;		    NPTS = IH(301+GNUM)
		    NPTS = NPOINTS
		    D(0)=D(0:NPTS-1)+IH(GNUM+101) ; WAVELENGTH OFFSET
		    IF NPTS LT 1022 THEN D(NPTS:1021)=0.0
		ENDIF
	ENDIF
   	IF I GT 1 THEN BEGIN		;SCALED DATA
   	  J=IH(I*4+14)		;SCALE FACTOR
   	  K=IH(I*4+15)
   	  D=D*J*2.0^(-K)		;UNSCALE
   	END; IF
	group(0,i)=d			;insert into group
    END; FOR I
;
; OPEN FITS FILE FOR WRITING
;
    IF NORDERS LE 1 THEN GROUP=GROUP(0:NPOINTS-1,*)
    IF GNUM EQ 1 THEN BEGIN
	FNAME=NAME+STRtrim(FILE,2)
	SxMAKE,1,FNAME,GROUP,0,NORDERS,HEAD
    END
    SxWRITE,1,GROUP
END
SKIPF,UNIT,1
CLOSE,1
END; LOOP ON FILES
;
FIN:
PRINT,'PROCESSING STOPPED AFTER FILE',FILE-1
;
RETURN
END
