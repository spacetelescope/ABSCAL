pro stisREDUCE,FILE,GRAT,aper,STAR,OUT=FILENAME,factor=fudge,merge=merge, $
		input=input,dith=dith
;+
;
; PURPOSE:
; 97jul21-COADD stis DATA AND WRITE ASCII OUTPUT FILES
; CALLING SEQUENCE:
;	stisREDUCE,FILE,GRAT,aper,STAR,OUT=filename,factor=fudge,merge=merge
; INPUT:
;	FILE-NAME OF log file OF stis OBSERVATIONS AS GENERATED BY stisDIR. MUST
;		start w/ 'dir' or 'DIR', else a list of obs to coadd
;		WITH GRATING NAMES IN THE FILE NAMES.
;		02may16-if file='' then merge existing files,eg QSO-J1019+27
;	GRAT-**VECTOR** list OF GRATING modes TO PROCESS (in merge order)
;		If file is a list of obs, then GRAT must NOT be vector, unless
;		grating name is in the filenames (as in MRGTRANSM.pro).
; OPTIONAL INPUT:
;	aper - restrict aperture selection. Use '' to get all apertures.
;	STAR-RESTRICT PROCESSING TO A CERTAIN STAR NAME *************
;	     OTHERWISE, ALL STARS GET ADDED TOGETHER!!!!!!!!!!!!!!!!!
;		Also, specifying star, makes a nice title, for normal use.
;	out=filename-special output file name
;	factor=fudge - fudge vector corresponding to grat vector. to fix (k648)
;	merge - keyword to merge spectra from diff gratings. default is NO merge
;		01apr3-if only one grating is spec with /merge, then all name.*
;		get merged.
;	input - keyword to specify input,output dir other than the default dat/.
;		Input applies only to CCD.??? see below
;	dith  - For dithered data, eg. obrr03*
; OUTPUT:
;	ASCII FILES OF THE COADDS & THE MERGING VIA stisMRG
; RESTRICTION in stisobs: all input files must have name dat/spec_rootname.fits
; EXAMPLE:
;	stisREDUCE,'DIR7063.LOG',['G230LB,'G430L'],'','GD153',OUT='dat/gd153'
; HISTORY
; 	97jul21 - R. BOHLIN
;	97aug18 - add capability to input list of obs to coadd, instead of dirxx
;	98feb27 - add merge capability
;	98jun2  - mods for mrgtransm W/ GRATINGS IN FILE NAMES do not work, in
;		general. So assume if a list is given along w/ only one grating,
;		then just assume list is ok. { somewhat flaky as if i want to
;		allow a list w/ grating names to be X-checked against a grating
;		vector, then i must specifiy at least 2 gratings to be checked!}
;	99sep16 - add nop3m3 keyword
;	99oct19 - co-add 230L & 230LB if both in grat vector
;	02may15 - add capability to merge existing grating spectra to make
;			separate -CCD and -MAMA merges
;	02sep3 - removed obsolete ff keyword, now that L-flats are implemented.
;	nop3m3 - keyword to avoid the corr of g140l from -3" to +3" for determ
;			of that ratio. See mrgall.pro
;	04mar9 - put EPOCH in header
;	14jul25-add keyword INPUT for input in g750lhgt11/, gwidth=11 extracts
;	15jun15-add dither keyword for obrr03*
;-

GRAT=STRUPCASE(GRAT)
if n_params(0) gt 3 then STAR=STRUPCASE(STAR)
st=''
!y.style=1  &  !x.style=1
!P.NOCLIP=1
hdr1=["SYS-ERROR is the broadband ~1% SYSTEMATIC UNCERTAINTY of STIS fluxes.", $
 "   Bohlin(2014,PASP,126,711). BOTH THE STAT-ERR AND SYS-ERR ARE 1-SIGMA."]
hdr2=[ '','',								     $
 " WAVELENGTH   COUNT-RATE     FLUX     STAT-ERROR   SYS-ERROR  NPTS "+      $
 "  TIME  QUAL",							     $
 " ###    1"]
nodata=0
file=strlowcase(file)
ngrat=N_ELEMENTS(GRAT)
gudgrat=grat
if n_params(0) le 2 then star=''		; move out of loop 02jun4
print,'STISREDUCE for Star/Gratings: ',star,grat
dum=filename
name=gettok(dum,'-')  &  nam=name		; for merging only
if strpos(filename,'qso') ge 0 then name=filename
if file(0) eq '' then goto,mergegrat
FOR IGRAT=0,ngrat-1 DO BEGIN		;MAIN LOOP
	hdr2(0)=''
; 2019jun13 - & omit ff for x1/
	if file(0) ne 'dirx1.log' then case grat(igrat) of
		'G140L': hdr2(0)=' Net & Flux corr '+			$
			'for change w/ time,temp (Bohlin,etal.2019,AJ,158,211)'
		'G230L': hdr2(0)=' Net & Flux corr. '+	$
			'for changes w/ time (Bohlin,etal.2019,AJ,158,211)'
		'G230LB': hdr2(0)='Net & Flux corr '+			$
		     'for time(2019,AJ,158,211)&CTE loss(2006,PASP,118,1455)'
		'G430L': hdr2(0)='Net and Flux corr '+		$
		     'for time(2019,AJ,158,211)&CTE loss(2006,PASP,118,1455)'
		'G750L': hdr2(0)='Net and Flux corr '+		$
		     'for time(2019,AJ,158,211)&CTE loss(2006,PASP,118,1455)'
		endcase
	if star eq 'HZ43' and grat(igrat) eq 'G750L' then		$
		hdr2(0)=grat(igrat)+' Net and Flux corr. for '+		$
			'time but NOT CTE loss, except o69u07030.'
; 98jun2 - patch to make old Low disp merges work for obsnames w/o grating names
	lst=strupcase(file)		; to make capital gratings work below
	if strpos(file(0),'dir') lt 0 and ngrat eq 1 then goto,nofidl
; start fiddling:
	if strpos(file(0),'dir') eq 0 then 				$
		stisobs,file,lst,GRTS,aps,STARS,GRAT(IGRAT),aper,STAR	$
	      else begin
		indx=where(strpos(lst,grat(igrat)) ge 0,ngood)
		if ngood gt 0 then lst=lst(indx)
		if grat(igrat) eq 'G230L' and ngood gt 0 then begin
			indx=where(strpos(lst,'G230LB') lt 0,ngood)
			if ngood gt 0 then lst=lst(indx)
			endif
; elim gratings w/ no data
		if ngood le 0 then begin
			good=where(gudgrat ne grat(igrat),ngood)
			if ngood gt 0 then gudgrat=gudgrat(good)
			goto,skipgrat      
			endif
		endelse
nofidl:
	lst=strlowcase(lst)
	if keyword_set(input) then				$
		for i=0,n_elements(lst)-1 do 			$
				lst(i)=replace_char(lst(i),'dat/',input)
	if lst(0) eq '' then begin
		print,'Skipping '+grat(igrat)+', which should have file already'
		goto,skipgrat
		endif
	print,'      adding: ',lst
	nodata=1				; data found to process
	dum=rem_dup(lst)			; 02may14 - idiot check
	if n_elements(dum) ne n_elements(lst) then stop
; stisadd puts in proper gwidth via stisflx
	stisadd,lst,HEAD,MT,EXP,W,C,F,E,NPTS,TIME,QUAL,dith=dith
	if keyword_set(filename) then begin
		name=strlowcase(filename)
		fdecomp,name,disk,dir,nam
	     end else begin
		NAME=strlowcase(strtrim(stars(0),2))
		nam=name
		endelse
	mt=nam+mt				; for ngc -a, etc star names
	if strpos(name,'.g') gt 1 then outnam=name else 	$	;kludge
				outnam=NAME+'.'+GRAT(IGRAT)
	CLOSE,11 & OPENW,11,strlowcase(outnam)
	PRINTF,11,'FILE WRITTEN BY STISREDUCE.PRO ON ',!STIME
	nlst=n_elements(lst)
	lst=lst(where(strmid(lst,0,1) ne 'B'))	; elim +3" after 1999.2
	ngood=n_elements(lst)
	if strpos(lst(0),'gaia') ge 0 then begin			$
		last=strpos(lst(0),'.fits')
		lst=strmid(lst,last-28,28)
		form='(a/(2a29))'
	    end	else begin
		fdecomp,lst,disk,dir,spcnam
		lst=strmid(spcnam,5,9)
		form='(a/(7(1x,a9)))'
		endelse
	if strpos(lst(0),'o') ne 0 then stop			; & fix
	printf,11,'coadd lst for '+GRAT(IGRAT)+' from dir='+input+':',	$
						lst,form=form
	iepoch=where(strpos(head,'EPOCH:') ge 0,nepoch)
	if nepoch gt 0 then printf,11,replace_char(head(iepoch),'HISTORY ','')
	if nlst ne ngood then printf,11,string(nlst-ngood,'(i3)')+	$
				' Bad spectra eliminated, eg +3" after 1999.2)'
;	MX=MAX(f(WHERE((W GT 1200) )))
	MX=MAX(f)			;97aug15 to make g140M-1173 work
	!mtitle=MT
	!ytitle='NET COUNTS/SEC'
	!ytitle='FLUX(10 !e-15!n erg s!e-1!n cm!e-2!n A!e-1!n)'
	!xtitle='wavelength (A)'
	plot,w,f,YR=[0,MX]
	plotdate,'stisreduce.pro'
;	oplot,[!CXMIN,!CXMAX],[0,0]
;	if !d.name eq 'X' then read,st
	NUMPTS=N_ELEMENTS(W)
	AP=STRTRIM(SXPAR(HEAD,'aperture'),2)
	det=strtrim(sxpar(head,'detector'),2)
	gwid=sxpar(head,'gwidth')
	sens=sxpar(head,'senstab')
	gwidstd=11					; 2019
	if file(0) eq 'dirx1.log' then begin		; MAST pipeline
		gwid=11  &  gwidstd=11
		if strtrim(sxpar(head,'detector'),2) eq 'CCD' then begin
			gwid=7  &  gwidstd=7
			endif
		endif
; check for any fudge factors
	if keyword_set(FUDGE) then begin
		f=f*fudge(igrat)
		if fudge(igrat) ne 1. then printf,11,grat(igrat),	$
		  ' flux multiplied by fudge=',fudge(igrat),form='(2a,f5.3)'
		endif
; 00apr6 - sys err = 1% internal residuals of abs cal (bohlin 2000 AJ) {was 2%}
	ind=where(c ne 0.)
	ferr=f*0  &  FERR(ind)=0.01*abs(F(ind))
; 2020mar15 - add sens to output
	printf,11,'gwidth for '+grat(igrat)+' flux cal=',		$
			strtrim(gwid,2),' w/ ',sens
	if ap eq '52X2' and gwid eq gwidstd then PRINTF,11,hdr1,format='(a)'  $
	     else begin
		printf,11,'SYS-ERROR is set to 1% but may be larger. Aper='+ap
		targname=strtrim(sxpar(head,'targname'),2)
		if targname eq 'HD172167-V6' then targname='HD172167'	;08dec11
		printf,11,targname
		specrow=sxpar(head,'soffset')+511
		printf,11,'Spectrum near image row=',string(specrow,'(f6.1)')
		endelse
	printf,11,hdr2
	printf,11,mt
	FMT='(F12.6,4E12.4,I4,F10.1,I4)'
	FOR I=0,NUMPTS-1 DO PRINTF,11,FORMAT=FMT,W(I),C(I),F(I),E(I),FERR(I), $
				     NPTS(I),TIME(I),QUAL(I)
	PRINTF,11,' 0 0 0 0 0 0 0 0'
	close,11
skipgrat:
	ENDFOR		;END MAIN LOOP
; return unless merging spectra
if (not keyword_set(merge)) or nodata eq 0 then return

;
; MERGE DATA AND WRITE IN .MRG FILE {note-some fluxes in overlap region change}
mergegrat:
;TITLE=STAR
title=strupcase(nam)						; try 03aug3
IF STAR EQ '' THEN TITLE=strtrim(stars(0),2)
; 00apr14 if keyword_set(filename) then title=nam		; 98may27
grat=gudgrat
; 01apr3 - merge all NAME.*, if only one grat is specified (for indiv lists)
if ngrat eq 1 then begin
	filgrat=findfile(name+'.*')
	good=where(strpos(filgrat,'.g') ge 0)  &  filgrat=filgrat(good)
	for i=0,n_elements(filgrat)-1 do begin
		fdecomp,filgrat(i),dsk,dr,nm,extent
		if i eq 0 then grat=extent else				$
				if extent ne 'mrg' then grat=[grat,extent]
		endfor
	endif

; 99oct19 - option to merge 230L & 230LB:
indl=where(grat eq 'G230L' or grat eq 'G230LB',ngood)
if ngood eq 2 then begin
	print,'Special merge of ',grat(indl)
	grat(indl(0))='G230MRG'  &  grat=grat(where(grat ne grat(indl(1))))
	stismrg230,name
	endif

IF N_ELEMENTS(GRAT) EQ 1 THEN TITLE=TITLE+' '+GRAT(0)		       $
	ELSE							       $
	TITLE=TITLE+' '+GRAT(0)+'+'+GRAT(1)

FIL1=NAME+'.'+GRAT(0) & FIL2=''		;SINGLE GRATING
IF N_ELEMENTS(GRAT) GT 1 THEN FIL2=NAME+'.'+GRAT(1)
; 98oct6 - use a different extent for Med disp merges
ext='.mrg'
if strpos(grat(0),'M-') gt 0 then ext='.mrg-m'
if strpos(filename,'-mama') gt 0 then ext='.mrg-mama'
if strpos(filename,'-ccd') gt 0 then ext='.mrg-ccd'
stismrg,strlowcase(fil1),strlowcase(fil2),strlowcase(name+ext),TITLE=TITLE  
IF N_ELEMENTS(GRAT) GE 3 THEN FOR IGRAT=2,N_ELEMENTS(GRAT)-1 DO	BEGIN
	TITLE=TITLE+'+'+GRAT(IGRAT)
	stismrg,name+ext,name+'.'+strlowcase(GRAT(IGRAT)),name+ext,TITLE=TITLE
	endfor

;READ BACK THE MERGED DATA WITH THE ASCII FILE READER
RDF,NAME+ext,1,DAT
W=DAT(*,0)
F=DAT(*,2)
STAT=DAT(*,3)
SYST=DAT(*,4)
;
;full PLOT:
!ytitle='FLUX & ERROR ARRAY (10 !e-15!n erg s!e-1!n cm!e-2!n A!e-1!n)'
flx=f*1.e15
MX=MAX(flx(WHERE((W GT 1200))))*1.2
err=1.e15*Stat
if max(w)/min(w) lt 5 then plot,w,flx,YR=[0,MX]			$
	else plot,w,flx,YR=[.0001*mx,MX],/ylog,/xlog
oplot,w,err,linestyle=2
if w(0) lt 4000 and !y.crange(0) gt 0 then xyouts,3800,.00007*mx,'4000'
if w(0) lt 2000 and !y.crange(0) gt 0 then xyouts,1900,.00007*mx,'2000'
plotdate,'stisreduce.pro'
!x.style=0
;if !d.name eq 'X' then READ,ST
END
